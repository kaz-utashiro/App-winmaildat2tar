#!/usr/bin/env perl

use v5.14;
use warnings;
use Convert::TNEF;
use Archive::Tar;

sub usage {
    die sprintf "Usage: %s winmail.dat\n", $0 =~ s|.*/||r;
}
@ARGV > 0 or usage;

sub unknown {
    my $seq = (state $_seq)++;
    sprintf "unknown%s.dat", $seq ? "_$seq" : "";
}

my $tar = Archive::Tar->new() or die $!;

for my $file (@ARGV) {
    my $tnef = Convert::TNEF->read_in($file, { output_to_core => 'ALL' })
	or die $Convert::TNEF::errstr;
    for my $ent ($tnef->attachments) {
	my $name = $ent->longname // $ent->name // unknown;
	$tar->add_data($name,
		       $ent->data,
		       { uname => 'nobody', gname => 'nogroup' });
    }
}

print $tar->write;

exit;

__END__

=encoding utf-8

=head1 NAME

winmaildat2tar - Convert winmail.dat (TNEF data) to Tar format

=head1 VERSION

Version 0.02

=head1 SYNOPSIS

$ winmaildat2tar winmail.dat | tar tvf -

=head1 DESCRIPTION

This command read C<winmail.dat> file in TNEF format and produce tar
archive formatted data.

=head1 INSTALL

=head2 CPANMINUS

To get the latest code, use this:

    $ cpanm https://github.com/kaz-utashiro/winmaildat2tar.git

=head1 SEE ALSO

L<https://github.com/kaz-utashiro/winmaildat2tar>

=head1 AUTHOR

Kazumasa Utashiro

=head1 LICENSE

Copyright 2020 Kazumasa Utashiro.

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut
