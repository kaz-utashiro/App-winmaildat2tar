#!/usr/bin/env perl

use v5.14;
use warnings;
use Convert::TNEF;
use Archive::Tar;

my $tar = Archive::Tar->new();
sub unknown {
    state $seq;
    sprintf "unknown%s.dat", $seq++ ? "_$seq" : "";
};

@ARGV or die "TNEF file is required.\n";

for my $file (@ARGV) {
    my $tnef = Convert::TNEF->read_in($file, { output_to_core => 'ALL' })
	or die $Convert::TNEF::errstr;
    for my $ent ($tnef->attachments) {
	my $name = $ent->longname // $ent->name // unknown;
	$tar->add_data($name,
		       $ent->data,
		       { uname => 'nobody', gname => 'nogroup' });
    }
}

print $tar->write;

exit;

__END__

=encoding utf-8

=head1 NAME

winmaildat2tar - Convert TNEF data (winmail.dat) to Tar format

=head1 VERSION

Version 0.01

=head1 SYNOPSIS

$ winmaildat2tar winmail.dat | tar tvf -

=head1 DESCRIPTION

This command read C<winmail.dat> file in TNEF format and produce tar
archive formatted data.

=head1 INSTALL

=head2 CPANMINUS

To get the latest code, use this:

    $ cpanm https://github.com/kaz-utashiro/App-winmaildat2tar.git

=head1 SEE ALSO

L<https://github.com/kaz-utashiro/App-winmaildat2tar>

=head1 AUTHOR

Kazumasa Utashiro

=head1 LICENSE

Copyright 2020 Kazumasa Utashiro.

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut
