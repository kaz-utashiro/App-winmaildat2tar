#!/usr/bin/env perl

use v5.14;
use warnings;
use Convert::TNEF;
use App::winmaildat2tar;

sub usage {
    die sprintf "Usage: %s winmail.dat\n", $0 =~ s|.*/||r;
}
@ARGV > 0 or usage;

use Getopt::EX::Long qw(:DEFAULT Configure ExConfigure);
ExConfigure BASECLASS => [ __PACKAGE__, "Getopt::EX" ];
Configure "bundling";
GetOptions(
    my $opt = {
	format => 'tar',
    },
    "format=s",
    );

sub unknown {
    my $seq = (state $_seq)++;
    sprintf "unknown%s.dat", $seq ? "_$seq" : "";
}

my $archive = App::winmaildat2tar::Archive->new($opt->{format});

for my $file (@ARGV) {
    my $tnef = Convert::TNEF->read_in($file, { output_to_core => 'ALL' })
	or die $Convert::TNEF::errstr;
    for my $ent ($tnef->attachments) {
	my $name = $ent->longname // $ent->name // unknown;
	$archive->add($name, $ent->data);
    }
}

print $archive->write;

exit;

__END__

=encoding utf-8

=head1 NAME

winmaildat2tar - Convert winmail.dat (TNEF data) to tentative archive

=head1 VERSION

Version 0.05

=head1 SYNOPSIS

$ winmaildat2tar [--format format] winmail.dat | tar tvf -

=head1 DESCRIPTION

This command read C<winmail.dat> file in TNEF format and produce
another tentative archive formatted data.  Defaut format is Tar.

=over 7

=item B<--format> I<format>

Specify archive format from B<Tar>, B<Ar> or B<Zip>.
Default is B<Tar>.

=back

=head1 INSTALL

=head2 CPANMINUS

To get the latest code, use this:

    $ cpanm https://github.com/kaz-utashiro/winmaildat2tar.git

=head1 SEE ALSO

L<https://github.com/kaz-utashiro/winmaildat2tar>

=head1 AUTHOR

Kazumasa Utashiro

=head1 LICENSE

Copyright 2020 Kazumasa Utashiro.

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut
