#!/usr/bin/env perl

use v5.14;
use warnings;
use Convert::TNEF;
use App::winmaildat2xar;

sub usage {
    die sprintf "Usage: %s winmail.dat\n", $0 =~ s|.*/||r;
}
@ARGV > 0 or usage;

use Getopt::EX::Long qw(:DEFAULT Configure ExConfigure);
ExConfigure BASECLASS => [ __PACKAGE__, "Getopt::EX" ];
Configure "bundling";
GetOptions(
    my $obj = {
	format => 'tar',
    },
    "format=s",
    );

sub unknown {
    my $seq = (state $_seq)++;
    sprintf "unknown%s.dat", $seq ? "_$seq" : "";
}

my $archive = App::winmaildat2xar::Archive->new($obj->{format});

for my $file (@ARGV) {
    my $tnef = Convert::TNEF->read_in($file, { output_to_core => 'ALL' })
	or die $Convert::TNEF::errstr;
    for my $ent ($tnef->attachments) {
	my $name = $ent->longname // $ent->name // unknown;
	$archive->add($name, $ent->data);
    }
}

print $archive->write;

exit;

__END__

=encoding utf-8

=head1 NAME

winmaildat2xar - Convert winmail.dat (TNEF data) to another archive

=head1 VERSION

Version 0.04

=head1 SYNOPSIS

$ winmaildat2xar winmail.dat | tar tvf -

=head1 DESCRIPTION

This command read C<winmail.dat> file in TNEF format and produce
another archive formatted data.  Only tar format is supported
currently.

=head1 INSTALL

=head2 CPANMINUS

To get the latest code, use this:

    $ cpanm https://github.com/kaz-utashiro/winmaildat2xar.git

=head1 SEE ALSO

L<https://github.com/kaz-utashiro/winmaildat2xar>

=head1 AUTHOR

Kazumasa Utashiro

=head1 LICENSE

Copyright 2020 Kazumasa Utashiro.

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut
